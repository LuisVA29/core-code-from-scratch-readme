# Miércoles 23 de noviembre
## 1. Algoritmo para la preparación de una pizza:

### Elementos de entrada:

1.	Agua caliente
2.	Levadura seca activa
3.	Harina de pan
4.	Aceite de oliva extra virgen
5.	Sal
6.	Azúcar
7.	Queso mozzarella 
8.	Salsa de tomate
9.	Pepperoni 
### Proceso:
1.	Inicio
2.	Colocar 1 taza y media en un bowl y agregar 2 cucharaditas de levadura y dejar reposar 5 min
3.	Luego de los 5 min, agregar 3 tazas y ¾ de harina, 2 cucharaditas de sal, 1 cucharadita de azúcar y 2 cucharadas de aceite de oliva y mezclar durante 1 minuto.
4.	Retirar la masa del bowl y amasar la masa de pizza en una superficie plana de 7 a 10 min
5.	Luego, volver a colocar la masa en el bowl y agregar aceite de oliva, luego cubrir la superficie del bowl con plástico y dejar reposar por 1 hora y media.
6.	Mientras se deja reposar la masa, precalentar el horno a 470° F o 246° Centígrados al menos 30 min
7.	Pasada la hora y media, remover el plástico del bowl y antes de tocar la masa espolvorear las manos con harina, luego dividir en 2 la masa.
8.	Formar 2 bolas con la masa y poner cada una en un bowl, cubrir con plástico y dejar reposar durante 15 min.
9.	Luego, tomar 1 bola, colocar en una superficie plana y comenzar a aplanar la masa con las manos de adentro hacia afuera. Debe tener un diámetro de 25 o 30 cm,
10.	Luego de aplanar la masa, colocarlo en una bandeja de pizza
11.	Agregar la salsa de tomate a la masa, espolvoree el queso y agregar los trozos de pepperoni sobre la pizza.
12.	Colocar la pizza dentro del horno
13.	Hornear la pizza a 475° F durante unos 10 o 15 min
14.	Pasado este tiempo, retirar la pizza del horno
15.	Cortar los trozos de pizza que desee
16.	Servir en un plato
17.	Fin


## 2. Algoritmo para convertir grados Celsius a Fahrenheit y viceversa
1.	Inicio
2.	Pedir la temperatura y llamarla x
3.	Definir “y”
4.	Definir “z”
5.	Preguntar si la temperatura es en grados Celsius
6.	Si la temperatura esta en grados Celsius: <br>
   6.1	realizar operación y = (x* 9/5) + 32 <br>
   6.2	Imprimir “y”<br>
7.	Si no son grados Celsius: <br>
   7.1	Realizar operación z = (x – 32) * 5/9 <br>
   7.2	Imprimir “z” <br>
8.	Fin 

## 3. Algoritmo para calcular el volumen  de una pirámide, un cubo y una esfera
1. Preguntar qué volumen se necesita (cubo, pirámide o esfera)
2. Si necesitamos un cubo, entonces:<br>
   2.1 Preguntar la longitud de un lado <br>
   2.2 Realizar operación volumen = lado^3 <br>
   2.3 Imprimir "volumen" <br>
3. Si necesitamos una esfera entonces: <br>
   3.1 Pedir la longitud del radio <br>
   3.2 Realizar operación volumen = 4/3 * pi * r^3 <br>
   3.3 Imprimir "volumen" <br>
4. Si se trata de una pirámide <br>
   4.1 Pedir el largo de la base y definir como "largo" <br>
   4.2 Pedir el ancho de la base y definir como "ancho"  <br>
   4.3 Pedir la altura y definir como "altura"  <br>
   4.4 Realizar operación volumen = 1/3 * largo * ancho * altura <br>
   4.5 Imprimir "volumen" <br> 
5. End

# Jueves 24 de noviembre

## 1. Algoritmo para comprobar si un número es par o impar
1.	Inicio
2.	Pedir numero
3.	Guardar numero en la variable “y”
4.	Definir x
5.	Realizar operación x = y/2
6.	Si el resultado es divisible dentro de 2<br>
6.1	Imprimir “El numero ingresado es un número par”<br>
7.	Si no,<br>
7.1	Imprimir “El numero ingresado es un numero impar”<br>
8.	Fin

![image](https://user-images.githubusercontent.com/66539558/203880476-cee708e8-ad27-4591-8144-1f2d154b00ab.png)
## 2. Algoritmo para calcular la edad de una persona usando su fecha de nacimiento
   1.	Inicio
2.	Pedir Año de nacimiento
3.	Guardad en “a”
4.	Pedir el año en que se encuentra
5.	Guardar en “b”
6.	Definir x
7.	Realizar operación x = b – a
8.	Imprimir “Usted tiene ”+ variable “x”
## 3. ¿Cuál de los cofres tiene el tesoro?
•	Cofre de la izquierda: dice que el cofre del medio tiene el tesoro, por lo tanto, el del medio no lo tiene.<br> 
•	Cofre del medio: dice que todos tienen un tesoro dentro, por lo tanto, esta diciendo que ningún cofre tiene un tesoro.<br> 
•	Cofre de la derecha: dice que solo uno tiene tesoros, por lo tanto, solamente hay dos cofres que tienen tesoros. <br> 
➡️Los cofres que tienen tesoros son el de la izquierda y la derecha, ya que anteriormente se confirmó que el del medio no tenía.⬅️
# Lunes 28 de noviembre
## 1. Problema lógico
•	Alice: dice que nadie estudió para el examen, entonces si nadie estudió ella tampoco lo hizo y es una contradicción. <br> 
•	Bob: dice que solo una persona estudió, no existe contradicción ya que esa persona es él. <br> 
•	Charlie: dice que dos personas estudiaron, es verdad ya que las personas que estudiaron son él y bob, pero no pueden existir dos verdades al mismo tiempo. <br> 
•	Dan: dice que tres personas estudiaron, pasa lo mismo que con Charlie. <br> 
•	Eva: dice que cuatro personas estudiaron, pasa lo mismo que con Charlie. <br> 
## 2. ¿Cuál va primero, cereal o leche? 
1.	Inicio
2.	Tomar un plato hondo de la alacena
3.	Tomar una cuchara del cajón
4.	Sacar la leche del refrigerador
5.	Preguntar si desea la leche caliente o fría:
1.	Si desea la leche fría:  <br> 
a.	Tomar la caja del cereal y servirlo en el plato hondo <br> 
b.	Verter la leche en el plato de cereal <br> 
2.	Si desea la leche caliente: <br> 
a.	Calentar la leche utilizando un horno microondas o una estufa <br> 
b.	Verter la leche dentro del plato hondo <br> 
c.	Tomar la caja de cereal y servirlo sobre el plato con leche. <br> 
6.	Fin

![Cereal or Milk](https://user-images.githubusercontent.com/66539558/204419173-0911a232-02cf-4eb0-a0c5-9fdfa2368e03.png)
# Martes 29 de noviembre 

## 1. Print my name
```python
Algoritmo myName
	Imprimir 'Mi nombre es Luis Velasquez'
FinAlgoritmo
```
## 2. Print my name & age
```python
Algoritmo myName
	Imprimir 'Mi nombre es Luis Velasquez'
	Imprimir 20
FinAlgoritmo
```
# Miércoles 30 de noviembre

## 1. Algorithm game
![GAME COMPLETE](https://user-images.githubusercontent.com/66539558/204934285-9f66938e-7791-4a84-bb5b-601b39a52692.png)
## 2. Mod in PSeInt Challenge
```python
Algoritmo EvenorOdd
	Escribir "Ingrese número"
	Leer num
	operacion1 = num % 2
	si	operacion1 = 0 Entonces
		Escribir "0"
	SiNo
		Escribir "1"
	FinSi
	
FinAlgoritmo
```
## 3. Register Form
```python
Algoritmo userform
	Escribir "======= USER FORM ======="
	Escribir "First name"
	Leer first
	Escribir "Last name"
	Leer last
	Escribir "Age"
	Leer age
	Escribir "e-mail"
	Leer email
	Escribir "Address"
	Leer address
	Imprimir "Your name is: ", first, " ", last
	Imprimir "Your age is: ", age
	Imprimir "Your e-mail address is: ",email
	Imprimir "Your address is: ", address
	Imprimir "Thank you for filling out the form!"
FinAlgoritmo
```
# Jueves 1 de diciembre
## Truth tables
1.	✅
2.	✅
3.	❌
4.	✅
5.	✅
6.	❌
7.	✅
8.	✅
9.	❌
10.	✅
11.	✅
12.	❌
13.	✅
14.	✅
## 2. Boolean Results

```python
Algoritmo boolean
	a <- 5 == 3
	// FALSE, because 5 is not equal to 3
	b <- 4 <> 3
	// TRUE, because 4 is different from 3
	c <- 7 > 7
	// FALSE, because 7 is not greater than 7.
	d <- 4 < 4
	// FALSE, because 4 is not less than 4.
	e <- 100 <= 90
	// FALSE, 100 is not less or equal to 90
	f <- 40 >= 40
	// TRUE, 40 is equal to 40
FinAlgoritmo
```
## 3. Identify odd and even numbers
```python
Algoritmo EvenorOdd
	Escribir "Ingrese número"
	Leer num
	operacion1 = num % 2
	si	operacion1 = 0 Entonces
		Escribir "Número ", num, " es par"
	SiNo
		Escribir "Número ", num, " es impar"
	FinSi
	
FinAlgoritmo
```
# Lunes 5 de diciembre
## 1. Simple Calculator
```python
Algoritmo SimpleCalculator
	Definir a, b, suma, resta, multi, div Como Real
	Definir opc Como Caracter
	Leer a,b
	Imprimir "¿Qué operación desea realizar? (ingresar +, -, *, /)"
	Imprimir "1. Suma ( + )"
	Imprimir "2. Resta ( - )"
	Imprimir "3. Multiplicación ( * )"
	Imprimir "4. División ( / )"
	Leer opc
	si	opc = "+" Entonces
		Imprimir "Realizando operación ", a, " + ",b
		suma = a+b
		Imprimir "El resultado de la suma es: ", suma
		SiNo
		Si	opc = "-" Entonces
			Imprimir "Realizando operación ", a, " - ",b
			resta = a-b
			Imprimir "El resultado de la resta es: ", resta	
		SiNo
		Si	opc = "*"	Entonces
			Imprimir "Realizando operación ", a, " * ",b
			multi = a*b
			Imprimir "El resultado de la multiplicación es: ", multi
		SiNo
			
		Si	opc = "/"	Entonces
			Imprimir "Realizando operación ", a, " / ",b
			div = a/b
			Imprimir "El resultado de la división es: ", div
		SiNo
			Imprimir "La operación no es válida?"
		FinSi
		FinSi
		FinSi
	FinSi
FinAlgoritmo

FinAlgoritmo
```
## 2. Special Number
```python
Algoritmo SpecialNumber
	Leer n
	Si n = 100 Entonces
		Imprimir 'This is a special number'
	FinSi
	Si (n % 10 == 0) & (n < 1000) & (n != 100) Entonces
		Imprimir 'This is almost special'
	SiNo
		Imprimir 'Just a regular number'
	FinSi
FinAlgoritmo
```
# Martes 6 de diciembre
## 1. Simple calculator (with switch)
```python
Algoritmo SimpleCalculatorv2
	Definir a, b, suma, resta, multi, div Como Real
	Definir opc Como Caracter
	Imprimir "====== Simple Calculator ======"
	Leer a,b
	Imprimir "¿Qué operación desea realizar? (ingresar +, -, *, /)"
	Imprimir "1. Suma ( + )"
	Imprimir "2. Resta ( - )"
	Imprimir "3. Multiplicación ( * )"
	Imprimir "4. División ( / )"
	Leer opc
	Segun opc Hacer
		"+" : 
			Imprimir "Realizando operación: ", a, " + ",b
			suma = a+b
			Imprimir "El resultado de la suma es: ", suma
		"-" :
			Imprimir "Realizando operación ", a, " - ",b
			resta = a-b
			Imprimir "El resultado de la resta es: ", resta
		"*" :
			Imprimir "Realizando operación ", a, " * ",b
			multi = a*b
			Imprimir "El resultado de la multiplicación es: ", multi
		"/" :
			si	b==0 Entonces
				Imprimir "La operación no es válida."
			SiNo
				div = a/b
				Imprimir "Realizando operación ", a, " / ",b
				Imprimir "El resultado de la división es: ", div
			FinSi
		De Otro Modo:
			Imprimir "La operación no es válida."
	FinSegun
FinAlgoritmo
```
##2. Multi Option Program
```python
Algoritmo MultiOptionP
	Imprimir "========== Multi Opción =========="
	Imprimir "Opciones disponibles: "
	Imprimir "1. Sumar de dos numeros"
	Imprimir "2. Imprimir dia de la semana"
	Imprimir "3. Calcular longitud de texto"
	Imprimir "Ingrese la opcion seleccionada:"
	Leer opc
	Segun opc Hacer
		
		1 :
			Imprimir "Ingrese los números que desea sumar: "
			Leer a,b
			suma = a+b
			Imprimir "La suma de los dos números es ", suma
		2 :
			Imprimir "Ingrese el número de la semana (del 1 al 7)"
			Leer dia
			Segun dia Hacer
				1:
					Imprimir "Domingo"
				2:
					Imprimir "Lunes"
				3:
					Imprimir "Martes"
				4:
					Imprimir "Miércoles"
				5:
					Imprimir "Jueves"
				6:
					Imprimir "Viernes"
				7:
					Imprimir "Sábado"
				De Otro Modo:
					Imprimir "El dato ingresado es inválido."
			FinSegun
		3 :
			Imprimir "Ingrese el texto:"
			Leer text
			Imprimir "La longitud del texto es de: " Longitud(text)
		De Otro Modo:
			Imprimir "La opción no está disponible."
	FinSegun
	Imprimir "========== Fin =========="
FinAlgoritmo
```
# Miércoles 7 de diciembre
## 1. Multiplication tables
```Python
Algoritmo MultiplicationTables
	Definir num, tabla Como Entero
	
	Escribir "========== Multiplication Tables =========="
	Escribir "Ingrese la tabla a calcular:"
	Leer tabla
	num=1
	Mientras num <= 10 Hacer
		Escribir tabla, " * ", num, " = ", num*tabla
		num = num+1
	Fin Mientras
FinAlgoritmo
```
## 2. Simple Calculator with Do While
``` Python
Algoritmo SimpleCalculatorv3
	Definir a, b, suma, resta, multi, div Como Real
	Definir opc Como Caracter
	Repetir
		Imprimir "============= Simple Calculator ============="
		Leer a,b
		Imprimir "¿Qué operación desea realizar? (ingresar +, -, *, /)"
		Imprimir "1. Suma ( + )"
		Imprimir "2. Resta ( - )"
		Imprimir "3. Multiplicación ( * )"
		Imprimir "4. División ( / )"
		Leer opc
		Segun opc Hacer
			"+" : 
				Imprimir "Realizando operación: ", a, " + ",b
				suma = a+b
				Imprimir "El resultado de la suma es: ", suma
			"-" :
				Imprimir "Realizando operación ", a, " - ",b
				resta = a-b
				Imprimir "El resultado de la resta es: ", resta
			"*" :
				Imprimir "Realizando operación ", a, " * ",b
				multi = a*b
				Imprimir "El resultado de la multiplicación es: ", multi
			"/" :
				si	b==0 Entonces
					Imprimir "La operación no es válida."
				SiNo
					div = a/b
					Imprimir "Realizando operación ", a, " / ",b
					Imprimir "El resultado de la división es: ", div
				FinSi
			De Otro Modo:
				Imprimir "La operación no es válida."
		FinSegun
		Imprimir "¿Desea realizar una nueva operación?"
		Imprimir "1. SI"
		Imprimir "2. NO"
		Leer opc2
	Hasta Que  opc2=2
	Imprimir "==================== FIN ===================="
FinAlgoritmo
```
# Jueves 8 de diciembre
## 1. Multiplication Tables with For
```Python
Algoritmo MultiplicationTablesv2
	Definir num Como Entero
	Repetir
		Escribir "=========== Multiplication Tables ============"
		Escribir "Ingrese la tabla a calcular"
		Leer num
		
		Para i<-0 Hasta 10 Con Paso 1 Hacer
			Escribir num, " * ", i, " = ", i*num
		Fin Para
		Escribir "¿Desea calcular otra tabla?"
		Escribir "1. Si"
		Escribir "2. No"
		leer opc
	Mientras Que opc = 1
	Escribir "==================== Fin ====================="

FinAlgoritmo
```
## 2. Ascending and Descending Numbers
```Python
Algoritmo AscendingandDescendingNumbers
	Definir num Como Real
	Repetir
	Escribir "====== Ascending and Descending Numbers ======"
	Escribir "Ingrese número"
	Leer num
	Escribir "Operaciones disponibles"
	Escribir "1. Imprimir en orden ascendente"
	Escribir "2. Imprimir en orden descendente"
	Escribir "Ingrese operación a ejecutar"
	Leer opc

	Segun opc Hacer
		1: 
			Para i<-0 Hasta num Con Paso 1 Hacer
				Imprimir i
			Fin Para
		2:
			Para i<-num Hasta 0 Con Paso -1 Hacer
				Imprimir i
			Fin Para

		De Otro Modo:
			Imprimir "Opción errónea"
	Fin Segun
	Escribir "¿Desea ingresar otro número?"
	Escribir "1. Si"
	Escribir "2. No"
	leer opc
Mientras Que opc = 1
Escribir "==================== Fin ====================="
FinAlgoritmo
```
## 3. Greetings
```Python
Algoritmo Greetings
	Definir saludos Como Entero
	Imprimir "========== Cheers =========="
	opc= "Si"
	saludos = 0
	Mientras opc = "Si"  Hacer
		Imprimir "Ingrese la hora actual (0-23):"
		Leer hora
		Si hora >=0 & hora<=12 Entonces
			Imprimir "Buenos dias"
		SiNo
			Si hora >=13 & hora<=18 Entonces
				Imprimir "Buenas tardes"
			SiNo
				Si hora >=19 & hora<=23 Entonces
					Imprimir "Buenas noches"
				SiNo
					Imprimir "Dato incorrecto"
				Fin Si
			Fin Si
		Fin Si
		saludos<-saludos+1;
		Imprimir "¿Desea continuar? Si/No"
		Leer opc
		
	Fin Mientras
	Escribir "Cantidad de saludos realizados: ",saludos
FinAlgoritmo
```
# Lunes 12 de diciembre
## 1. Average sales and commission
``` Python
Algoritmo SalesandCommissions
	Escribir "Write the total number of sales to enter:"
	Leer sales
	i=0
	suma = 0
	Para i<-1 Hasta sales Con Paso 1 Hacer
		Escribir "Write the value of the sale number:", i
		Leer value
		suma= suma+value
	Fin Para
	Escribir "The average sales is $", suma/sales
	Si sales>5 Entonces
		Escribir 'The total amount of the sales is: ', suma
		Escribir "The commission received by the seller is: $", suma*0.15
	SiNo
		Escribir "The commission received by the seller is: $", suma*0.10
	Fin Si
FinAlgoritmo
```
## 2. Even or Odd 
``` Python
Algoritmo EvenordOddv2
	
	Repetir
		Escribir "Write a nunber between 1 and 50"
		Leer data
		operacion = data%2
		Si data >= 1 & data <=50 Entonces
			Si operacion=0 Entonces
				Para secuencia<-0 Hasta data Con Paso 2 Hacer
					Escribir secuencia
				Fin Para
			SiNo
				Para secuencia<-1 Hasta data Con Paso 2 Hacer
					Escribir secuencia
				Fin Para
			Fin Si
			
		SiNo
			Escribir "Invalid number"
		Fin Si
	Mientras Que data>50 | data<=0

FinAlgoritmo
```
# Martes 13 de diciembre
## 1. Full name
``` Python
Algoritmo FullName
	Escribir "Write a name"
	Leer first
	Escribir "Write a last name"
	Leer last
	Dimension cadena1[2]
	cadena1[0] = first
	cadena1[1] = last
	Imprimir Mayusculas(SubCadena(cadena1[0],0,0)), Minusculas(SubCadena(cadena1[0],1,999)), ' ',Mayusculas(SubCadena(cadena1[1],0,0)), Minusculas(SubCadena(cadena1[1],1,999))
FinAlgoritmo
```
## 2. Throw dice 🎲 
``` python
Algoritmo ThrowDice
	Para repetir1<-1 Hasta 10 Con Paso 1 Hacer
		valor1 = Aleatorio(1,6)
		valor2 = Aleatorio(1,6)
		Si valor1 = valor2 Entonces
			Escribir valor1, ' ', valor2, 'The dice are the same.'
		SiNo
			Escribir valor1, ' ', valor2
		Fin Si
	Fin Para
FinAlgoritmo
```
